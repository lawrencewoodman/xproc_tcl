package require tcltest
namespace import tcltest::*

# Add module dir to tm paths
set ThisScriptDir [file dirname [info script]]
set ModuleDir [file normalize [file join $ThisScriptDir ..]]
::tcl::tm::path add $ModuleDir

package require xproc

xproc::proc addNums2 {a b} {
  expr {$a+$b}
} -description {
  Add two numbers together and return the result

  This could work as follows:
    addNums2 7 9      ; # Would return 16
} -test {{t} {
  set cases {
    {input {7 9} want 16}
    {input {2 3} want 18}
    {input {2 4} want 6}
  }
  set i 0
  foreach c $cases {
    dict with c {
      set got [addNums2 {*}$input]
      if {$got != $want} {
        xproc::testFail $t "($i) got: $got, want: $want"
      }
    }
    incr i
  }
}}


xproc::proc addNums3 {a b} {
  expr {$a+$b}
} -description {
  Add two numbers together and return the result

  This could work as follows:
    addNums3 7 9      ; # Would return 16
} -test {{t} {
  set cases {
    {input {7 9} want 15}
    {input {2 3} want 5}
    {input {2 4} want 7}
  }
  xproc::testCases $t $cases {{input} {addNums3 {*}$input}}
}}


xproc::proc addNums4 {a b} {
  expr {$a+$b}
} -description {
  Add two numbers together and return the result

  This could work as follows:
    addNums4 7 9      ; # Would return 16
} -test {{t} {
  set cases {
    {input {7 9} want 16}
    {input {2 3} want 5}
    {input {2 4} want 6}
  }
  xproc::testCases $t $cases {{input} {addNums4 {*}$input}}
}}


proc addNums5 {a b} {
  expr {$a+$b}
}
xproc::describe addNums5 {
  Add two numbers together and return the result

  This could work as follows:
    addNums5 7 9      ; # Would return 16
}
xproc::test addNums5 {{t} {
  set cases {
    {input {7 9} want 16}
    {input {2 3} want 18}
    {input {2 4} want 6}
  }
  xproc::testCases $t $cases {{input} {addNums5 {*}$input}}
}}


xproc::proc addNums6 {a b} {
  expr {$a+$b}
} -description {
  Add two numbers together and return the result

  This could work as follows:
    addNums6 7 9      ; # Would return 16
} -test {{t} {
  set cases {
    {input {7 9} want 16}
    {input {2 3} want 17}
    {input {2 4} want 4}
    {input {2 4} want 9}
  }
  set i 0
  foreach c $cases {
    if {$i == 3} {
      error "Time to fail - i: $i"
    }
    dict with c {
      set got [addNums2 {*}$input]
      if {$got != $want} {
        xproc::testFail $t "($i) got: $got, want: $want"
      }
    }
    incr i
  }
}}


xproc::proc subNums {a b} {
  expr {$a-$b}
} -description {
  Subtract one number from another and return the result

  This could work as follows:
    subNums 7 9      ; # Would return -2
} -test {{t} {
  set cases {
    {input {7 9} want -2}
    {input {5 4} want 1}
    {input {2 4} want -2}
    {input {4 4} want 0}
  }
  xproc::testCases $t $cases {{input} {subNums {*}$input}}
}}


xproc::proc raiseErrorCmd {a b} {
  expr {$a-$c}
} -description {
  Raises an error because $c doesn't exist
} -test {{t} {
  raiseErrorCmd 4 5
}}


namespace eval aTestNamespace {
  xproc::proc cmdA {} {
    return "defined inside eval aTestNamespace"
  } -description {
    this is a description of cmdA
  } -test {{t} {
    set got [cmdA]
    set want "defined inside eval aTestNamespace"
    if {$got ne $want} {
      xproc::testFail $t "got: $got, want: $want"
    }
  } aTestNamespace}
}

xproc::proc aTestNamespace::cmdB {} {
  return "defined outside eval aTestNamespace"
} -description {
  this is a description of cmdB
} -test {{t} {
  set got [aTestNamespace::cmdB]
  set want "defined outside eval aTestNamespace"
  if {$got ne $want} {
    xproc::testFail $t "got: $got, want: $want"
  }
}}


xproc::proc ::aTestNamespace::cmdC {} {
  return "defined outside eval aTestNamespace"
} -description {
  this is a description of cmdC
} -test {{t} {
  set got [aTestNamespace::cmdC]
  set want "defined outside eval aTestNamespace"
  if {$got ne $want} {
    xproc::testFail $t "got: $got, want: $want"
  }
}}


# This shouldn't appear in tests or descriptions because is removed in both
namespace eval bTestNamespace {
  xproc::proc cmdA {} {
    return "defined inside eval bTestNamespace"
  } -description {
    this is a description of cmdA
  } -test {{t} {
    set got [cmdA]
    set want "defined inside eval bTestNamespace"
    if {$got ne $want} {
      xproc::testFail $t "got: $got, want: $want"
    }
  } bTestNamespace}
  xproc::proc cmdB {} {
    return "defined inside eval bTestNamespace"
  } -description {
    this is a description of cmdB
  } -test {{t} {
    set got [cmdB]
    set want "defined inside eval bTestNamespace"
    if {$got ne $want} {
      xproc::testFail $t "got: $got, want: $want"
    }
  } bTestNamespace}

}


# This shouldn't appear in tests because is removed
namespace eval cTestNamespace {
  xproc::proc cmdA {} {
    return "defined inside eval cTestNamespace"
  } -description {
    this is a description of cmdA
  } -test {{t} {
    set got [cmdA]
    set want "defined inside eval cTestNamespace"
    if {$got ne $want} {
      xproc::testFail $t "got: $got, want: $want"
    }
  } cTestNamespace}
}


# This shouldn't appear in descriptions because is removed
namespace eval dTestNamespace {
  xproc::proc cmdA {} {
    return "defined inside eval dTestNamespace"
  } -description {
    this is a description of cmdA
  } -test {{t} {
    set got [cmdA]
    set want "defined inside eval dTestNamespace"
    if {$got ne $want} {
      xproc::testFail $t "got: $got, want: $want"
    }
  } dTestNamespace}
}

# Remove relevant namespaces as described above
xproc::remove tests ::bTestName* ::cTestName*
xproc::remove descriptions ::bTestName* ::dTestName*


test proc-1 {Allows normal definition of a proc} -body {
  xproc::proc addNums {a b} {expr {$a+$b}}
  addNums 7 9
} -result {16}


test runTests-1 {Provide correct test output} -body {
  xproc::runTests
} -output [string map [list !openBrace! "\{"] {--- FAIL  ::addNums2
---       (1) got: 5, want: 18
--- FAIL  ::addNums3
---       (0) got: 16, want: 15
--- FAIL  ::addNums3
---       (2) got: 6, want: 7
--- FAIL  ::addNums5
---       (1) got: 5, want: 18
--- FAIL  ::addNums6
---       (1) got: 5, want: 17
--- FAIL  ::addNums6
---       (2) got: 6, want: 4
--- FAIL  ::addNums6
---       Time to fail - i: 3
              while executing
          "error "Time to fail - i: $i""
              (lambda term "{t} !openBrace!
            set cases !openBrace!
              {input {7 9} want 16}
              !openBrace!input !openBrace!2 ..." line 11)
              invoked from within
          "apply !openBrace!{t} !openBrace!
            set cases {
              {input {7 9} want 16}
              {input {2 3} want 17}
              {input {2 4} want 4}
              {input {2 4} want 9}
            }
            set i 0
            forea..."
              ("uplevel" body line 1)
              invoked from within
          "uplevel 1 [list apply $lambda $commandName]"
--- FAIL  ::raiseErrorCmd
---       can't read "c": no such variable
              while executing
          "expr {$a-$c}"
              (procedure "raiseErrorCmd" line 2)
              invoked from within
          "raiseErrorCmd 4 5"
              (lambda term "{t} {
            raiseErrorCmd 4 5
          }" line 2)
              invoked from within
          "apply {{t} {
            raiseErrorCmd 4 5
          }} ::raiseErrorCmd"
              ("uplevel" body line 1)
              invoked from within
          "uplevel 1 [list apply $lambda $commandName]"

Total: 16,  Passed: 11,  Failed: 5
}] -result 5


test runTests-2 {Provide extra test output with -verbose} -body {
  xproc::runTests -verbose
} -output [string map [list !openBrace! "\{"] {=== RUN   ::xproc::MakeSummary
--- PASS  ::xproc::MakeSummary
=== RUN   ::xproc::IndentEachLine
--- PASS  ::xproc::IndentEachLine
=== RUN   ::xproc::CountIndent
--- PASS  ::xproc::CountIndent
=== RUN   ::xproc::StripIndent
--- PASS  ::xproc::StripIndent
=== RUN   ::xproc::TidyDescription
--- PASS  ::xproc::TidyDescription
=== RUN   ::addNums2
--- FAIL  ::addNums2
---       (1) got: 5, want: 18
=== RUN   ::addNums3
--- FAIL  ::addNums3
---       (0) got: 16, want: 15
--- FAIL  ::addNums3
---       (2) got: 6, want: 7
=== RUN   ::addNums4
--- PASS  ::addNums4
=== RUN   ::addNums5
--- FAIL  ::addNums5
---       (1) got: 5, want: 18
=== RUN   ::addNums6
--- FAIL  ::addNums6
---       (1) got: 5, want: 17
--- FAIL  ::addNums6
---       (2) got: 6, want: 4
--- FAIL  ::addNums6
---       Time to fail - i: 3
              while executing
          "error "Time to fail - i: $i""
              (lambda term "{t} !openBrace!
            set cases !openBrace!
              {input {7 9} want 16}
              !openBrace!input !openBrace!2 ..." line 11)
              invoked from within
          "apply !openBrace!{t} !openBrace!
            set cases {
              {input {7 9} want 16}
              {input {2 3} want 17}
              {input {2 4} want 4}
              {input {2 4} want 9}
            }
            set i 0
            forea..."
              ("uplevel" body line 1)
              invoked from within
          "uplevel 1 [list apply $lambda $commandName]"
=== RUN   ::subNums
--- PASS  ::subNums
=== RUN   ::raiseErrorCmd
--- FAIL  ::raiseErrorCmd
---       can't read "c": no such variable
              while executing
          "expr {$a-$c}"
              (procedure "raiseErrorCmd" line 2)
              invoked from within
          "raiseErrorCmd 4 5"
              (lambda term "{t} {
            raiseErrorCmd 4 5
          }" line 2)
              invoked from within
          "apply {{t} {
            raiseErrorCmd 4 5
          }} ::raiseErrorCmd"
              ("uplevel" body line 1)
              invoked from within
          "uplevel 1 [list apply $lambda $commandName]"
=== RUN   ::aTestNamespace::cmdA
--- PASS  ::aTestNamespace::cmdA
=== RUN   ::aTestNamespace::cmdB
--- PASS  ::aTestNamespace::cmdB
=== RUN   ::aTestNamespace::cmdC
--- PASS  ::aTestNamespace::cmdC
=== RUN   ::dTestNamespace::cmdA
--- PASS  ::dTestNamespace::cmdA

Total: 16,  Passed: 11,  Failed: 5
}] -result 5


test descriptions-1 {Return descriptions} -body {
  xproc::descriptions
} -result [dict create ::addNums2 {Add two numbers together and return the result

This could work as follows:
  addNums2 7 9      ; # Would return 16} \
::addNums3 {Add two numbers together and return the result

This could work as follows:
  addNums3 7 9      ; # Would return 16} \
::addNums4 {Add two numbers together and return the result

This could work as follows:
  addNums4 7 9      ; # Would return 16} \
::addNums5 {Add two numbers together and return the result

This could work as follows:
  addNums5 7 9      ; # Would return 16} \
::addNums6 {Add two numbers together and return the result

This could work as follows:
  addNums6 7 9      ; # Would return 16} \
::subNums {Subtract one number from another and return the result

This could work as follows:
  subNums 7 9      ; # Would return -2} \
::raiseErrorCmd {Raises an error because $c doesn't exist} \
::aTestNamespace::cmdA {this is a description of cmdA} \
::aTestNamespace::cmdB {this is a description of cmdB} \
::aTestNamespace::cmdC {this is a description of cmdC} \
::cTestNamespace::cmdA {this is a description of cmdA}]


cleanupTests
